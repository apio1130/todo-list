plugins {
	id 'org.springframework.boot' version '2.2.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	// id 'java'
    id "com.moowork.node" version "1.3.1"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    
    group = 'com.todolist'
    version = '1.0'
    sourceCompatibility = '1.8'

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    task initSourceFolders {
        sourceSets*.java.srcDirs*.each {
            if (!it.exists()) {
                it.mkdirs()
            }
        }
        sourceSets*.resources.srcDirs*.each {
            if (!it.exists()) {
                it.mkdirs()
            }
        }
    }
    
    test {
        useJUnitPlatform()
    }
}

project(':todo-list-api') {
    dependencies {
        implementation project(':todo-list-common')
    }
}

project(':todo-list-web') {
    apply plugin: "com.moowork.node"

    node {
        version = '12.11.1'
        npmVersion = '6.13.4'
        download = false
        // npm, node 설치파일 저장 디렉토리 명시
        // workDir = file("./src/front")  // node
        // npmWorkDir = file("./src/front") // npm
        nodeModulesDir = file("./src/front")
    }

    task setUp(type: NpmTask) {
        description = "Install Node.js package"
        args = ['install']
        inputs.files file('package.json')
        outputs.files file('node_modules')
    }

    task buildFront(type: NpmTask, dependsOn: setUp) {
        description = "Build vue.js"
        args = ['run', 'build']
    }

    processResources.dependsOn 'buildFront'

	dependencies {
		implementation project(':todo-list-common')
	}

}